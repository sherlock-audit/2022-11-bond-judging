xiaoming90

medium

# Inconsistency Of Minimum And Maximum Terms Allowed

## Summary

Inconsistency of the minimum and maximum terms allowed for a bond token deployed through Bond Protocol might cause issues and be error-prone.

## Vulnerability Detail

A 'vesting' param longer than 50 years is considered a timestamp for fixed expiry based on the following comment within the codebase.

https://github.com/sherlock-audit/2022-11-bond/blob/main/src/bases/BondBaseSDA.sol#L98

```solidity
File: BondBaseSDA.sol
97:     // A 'vesting' param longer than 50 years is considered a timestamp for fixed expiry.
98:     uint48 internal constant MAX_FIXED_TERM = 52 weeks * 50;
```

Within the `BondFixedTermSDA.createMarket` function, validation is in place at Line 38 to prevent users from creating a market that issues fixed-term bonds that vest less than 1 day or more than `MAX_FIXED_TERM` (50 years).

This shows that the protocol does not intend to support fixed-term bonds that vest less than 1 day or more than `MAX_FIXED_TERM` (50 years).

https://github.com/sherlock-audit/2022-11-bond/blob/main/src/BondFixedTermSDA.sol#L33

```solidity
File: BondFixedTermSDA.sol
33:     function createMarket(bytes calldata params_) external override returns (uint256) {
34:         // Decode params into the struct type expected by this auctioneer
35:         MarketParams memory params = abi.decode(params_, (MarketParams));
36: 
37:         // Check that the vesting parameter is valid for a fixed-term market
38:         if (params.vesting != 0 && (params.vesting < 1 days || params.vesting > MAX_FIXED_TERM))
39:             revert Auctioneer_InvalidParams();
40: 
41:         // Create market and return market ID
42:         return _createMarket(params);
43:     }
```

If there is any bond that has a vesting period longer than 50 years, it is considered a timestamp for fixed expiry, and the bond will be considered a fixed-expiry bond. Many parts of the protocol rely on this invariant to determine whether a bond is a fixed-term or fixed-expiry.

The `BondBaseSDA.isInstantSwap` function determines if a bond is fixed-term or fixed-expiry by comparing it against the `MAX_FIXED_TERM` in Line 782

https://github.com/sherlock-audit/2022-11-bond/blob/main/src/bases/BondBaseSDA.sol#L780

```solidity
File: BondBaseSDA.sol
780:     function isInstantSwap(uint256 id_) public view returns (bool) {
781:         uint256 vesting = terms[id_].vesting;
782:         return (vesting <= MAX_FIXED_TERM) ? vesting == 0 : vesting <= block.timestamp;
783:     }
```

The `BondAggregator.findMarketFor` function determines if a bond is fixed-term or fixed-expiry by comparing it against the `MAX_FIXED_TERM` in Line 241

https://github.com/sherlock-audit/2022-11-bond/blob/main/src/BondAggregator.sol#L221

```solidity
File: BondAggregator.sol
221:     function findMarketFor(
222:         address payout_,
223:         address quote_,
224:         uint256 amountIn_,
225:         uint256 minAmountOut_,
226:         uint256 maxExpiry_
227:     ) external view returns (uint256) {
228:         uint256[] memory ids = marketsFor(payout_, quote_);
229:         uint256 len = ids.length;
230:         uint256[] memory payouts = new uint256[](len);
231: 
232:         uint256 highestOut;
233:         uint256 id = type(uint256).max; // set to max so an empty set doesn't return 0, the first index
234:         uint48 vesting;
235:         uint256 maxPayout;
236:         IBondAuctioneer auctioneer;
237:         for (uint256 i; i < len; ++i) {
238:             auctioneer = marketsToAuctioneers[ids[i]];
239:             (, , , , vesting, maxPayout) = auctioneer.getMarketInfoForPurchase(ids[i]);
240: 
241:             uint256 expiry = (vesting <= MAX_FIXED_TERM) ? block.timestamp + vesting : vesting;
242: 
243:             if (expiry <= maxExpiry_) {
244:                 payouts[i] = minAmountOut_ <= maxPayout
245:                     ? payoutFor(amountIn_, ids[i], address(0))
246:                     : 0;
247: 
248:                 if (payouts[i] > highestOut) {
249:                     highestOut = payouts[i];
250:                     id = ids[i];
251:                 }
252:             }
253:         }
254: 
255:         return id;
256:     }
```

However, the issue is that it is possible for users to deploy a fixed-term bond that is more than 50 years (e.g. 100 years) because the `BondFixedTermTeller.deploy` function does not verify that the vesting period is less than 50 years before creating a token.

https://github.com/sherlock-audit/2022-11-bond/blob/main/src/BondFixedTermTeller.sol#L175

```solidity
File: BondFixedTermTeller.sol
172:     /* ========== TOKENIZATION ========== */
173: 
174:     /// @inheritdoc IBondFixedTermTeller
175:     function deploy(ERC20 underlying_, uint48 expiry_)
176:         external
177:         override
178:         nonReentrant
179:         returns (uint256)
180:     {
181:         uint256 tokenId = getTokenId(underlying_, expiry_);
182:         // Only creates token if it does not exist
183:         if (!tokenMetadata[tokenId].active) {
184:             _deploy(tokenId, underlying_, expiry_);
185:         }
186:         return tokenId;
187:     }
```

Once the users create the fixed-term bond (e.g. 100 years fixed-term), they can proceed to call the `BondFixedTermTeller.create` to mint the fixed-term bond for distribution to the public.

## Impact

This inconsistency will cause some issues and be error-prone when implementing logic to handle fixed-term bonds created by users VS fixed-term bonds minted by the market. It might also cause issues and confusion when other protocols attempt to integrate with Bond protocol's tokens. Since the protocol deems any bond that has a vesting period longer than 50 years to be considered a timestamp for fixed expiry, a fixed-term bond with a vesting period longer than 50 years might be wrongly deemed as a fixed expiry bond.

## Code Snippet

https://github.com/sherlock-audit/2022-11-bond/blob/main/src/BondFixedTermTeller.sol#L175

## Tool used

Manual Review

## Recommendation

Ensure that the minimum and maximum terms allowed for a bond token deployed through Bond Protocol are consistent.

```diff
function deploy(ERC20 underlying_, uint48 expiry_)
    external
    override
    nonReentrant
    returns (uint256)
{
+	if (expiry_ != 0 && (expiry_ < 1 days || expiry_ > MAX_FIXED_TERM))
+		revert Auctioneer_InvalidParams();
+
    uint256 tokenId = getTokenId(underlying_, expiry_);
    // Only creates token if it does not exist
    if (!tokenMetadata[tokenId].active) {
    	_deploy(tokenId, underlying_, expiry_);
    }
    return tokenId;
}
```

Alternatively, instead of determining if a bond is fixed-term or fixed-expiry by comparing it against the `MAX_FIXED_TERM` that is error-prone, consider having a state variable within the bond token implementation that stores a magic predefined byte4 value that indicates whether a bond token is a fixed-term or fixed-expiry token that is more reliable.